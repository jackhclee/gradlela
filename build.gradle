plugins {
    id "java"
    id "application"
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

ext {
    cucumberVer = "7.9.0"
    junit5Ver = "5.9.1"
}

dependencies {
    implementation     group: "commons-io",               name: "commons-io",                     version: "2.11.0"
    implementation     group: "org.springframework.boot", name: "spring-boot-starter-parent",     version: "2.7.5"
    implementation     group: "org.springframework.boot", name: "spring-boot-starter-web"
    implementation     group: "org.springframework.boot", name: "spring-boot-starter-actuator"
    implementation     group: "org.springframework.boot", name: "spring-boot-starter-security"
    testImplementation group: "org.junit.jupiter",        name: "junit-jupiter",                  version: "${junit5Ver}"
    testRuntimeOnly    group: "org.junit.jupiter",        name: "junit-jupiter-engine",           version: "${junit5Ver}"
    testImplementation group: "io.cucumber",              name: "cucumber-java",                  version: "${cucumberVer}"
    testImplementation group: "io.cucumber",              name: "cucumber-junit-platform-engine", version: "${cucumberVer}"
}

application {
    mainClass = "Main"
}

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    //'--glue', 'StepDefinitions',
                    'src/test/resources']
        }
    }
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }

}
